ini = { SOI ~ (pair | comment)* ~ section* }

// lines
pair = { key ~ "=" ~ value ~ eol}
comment = { ("#" | ";") ~ value? ~ eol}

// sections
section = { "[" ~ name ~ "]" ~ eol ~ body? }
body = { (pair | comment)* }

// tokens
// a name must end with a closing bracket followed by EOL
name = @{ (!("]" ~ eol) ~ char)+ }
// a key may not begin with an opening bracket and my not contain an equal sign
key = @{ (!("=" | "[") ~ char) ~ (!"=" ~ char){0, 49} }
value = @{ char{1, 255} }

// atomic primitives
char = { !eol ~ ANY }
eol = _{ NEWLINE | EOI }

WHITESPACE = _{ " " | "\t" }
