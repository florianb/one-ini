ini = { SOI ~ (blank | pair | comment)* ~ section* }

// lines
blank = _{ NEWLINE }
pair = { key ~ "=" ~ value ~ eol}
comment = { indicator ~ value? ~ eol}

// sections
section = { "[" ~ name ~ "]" ~ eol ~ body? }
body = { (blank | pair | comment)* }

// tokens
// a name must end with a closing bracket followed by EOL
name = @{ (!("]" ~ eol) ~ char)+ }
// a key may not begin with an opening bracket and my not contain an equal sign
key = @{ (!("=" | "[") ~ char) ~ (!"=" ~ char){0, 49} }
value = @{ char{0, 255} }

// atomic primitives
indicator = { "#" | ";" }
char = { !eol ~ ANY }
eol = _{ NEWLINE | EOI }

WHITESPACE = _{ " " | "\t" }
